#! /usr/bin/perl

use strict;
use warnings;

use Getopt::Long qw(:config auto_version); # Get options
use Pod::Usage;   # Printing pod documentation in terminal
use File::Spec;
use Cwd 'abs_path';
use Data::Dumper;
use Tie::RefHash;
use List::Util qw(first);

use CracTools::Utils 1.24;
use CracTools::Output;
use CracTools::SimCT;
use CracTools::SimCT::Const;

=head1 SYNOPSIS

simCT -g reference_genome/ -a annotations.gtf [-o my_simulation]

=head1 OPTIONS

=head2 General

  --help                  Print this help
  --man                   Open man page
  -p                      Number of threads/processes

=head2 Input / Output

  -o,--output-dir         Output directory (DEFAULT: 'simCT_simulation')
  -g,--genome-dir         Reference genome directory (with chromosomes splited in individual FASTA files)
  -a,--annotations        Annotations file in GTF format

=head2 Germline genome simulation

  -s,--substitution-rate  Rate a wich substitutions are randomly inserted in the reference genome
  -i,--insertion-rate     Rate a wich insertions are randomly inserted in the reference genome
  -d,--deletion-rate      Rate a wich deletions are randomly inserted in the reference genome

=head2 Clones genome simulation

  --clone-substitution-rate Rate a wich substitutions are randomly inserted in the germline genome (DEFAULT 0.001)
  --nb_generations          Number of generations of sub-clones (DEFAULT: 4)

=head2 Others

  --disable-error-encoding    Remove error encoding from read names
  --uniq-ids                  Read names are identical for both pairs

=cut

my ($help,$man,$verbose);
my ($disable_error_encoding,$uniq_ids,$single_end);
my ($genome_dir,$gtf_file);
my $sub_rate               = $CracTools::SimCT::Const::SUB_RATE;
my $ins_rate               = 0;
my $del_rate               = 0;
my $output_dir             = $CracTools::SimCT::Const::OUTPUT_DIRECTORY;
my $max_splice_length      = $CracTools::SimCT::Const::MAX_SPLICE_LENGTH;
my $vcf_ratio  = 0.8;
my $nb_fusions = 0;
my $nb_process = 1;
my $nb_generations = 4;
my $clone_sub_rate = 0.001;

my @ARGV_copy = @ARGV;

GetOptions( "v|verbose"             => \$verbose,
            "man"                   => \$man,
            "help"                  => \$help,
            "p=i"                   => \$nb_process,

            # Input / Output
            "o|output-dir=s"        => \$output_dir,
            "g|genome-dir=s"        => \$genome_dir,
            "a|annotations=s"       => \$gtf_file,

            # SimCT parameters
            "disable-error-encoding"=> \$disable_error_encoding,
            "uniq-ids"              => \$uniq_ids,

            # Germline mutations parameters
            "s|substitution-rate=f" => \$sub_rate,
            "i|insertion-rate=f"    => \$ins_rate,
            "d|deletion-rate=f"     => \$del_rate,

            # Clone parameters
            "clone-substitution-rate=f" => \$clone_sub_rate,
            "nb-generations=i"          => \$nb_generations,

          ) or pod2usage(-verbose => 1);

pod2usage(-verbose => 1)  if ($help);
pod2usage(-verbose => 2)  if ($man);

pod2usage(
  -message => "Mandatory argument '-genome-dir' is missing",
  -verbose => 1,
) unless defined $genome_dir;

pod2usage(
  -message => "Mandatory argument '-annotations' is missing",
  -verbose => 1,
) unless defined $gtf_file;

require CracTools::SimCT::Genome;
require CracTools::SimCT::Annotations;
require CracTools::SimCT::GenomeSimulator;
require CracTools::SimCT::MutationGenerator::Random;

# Create the reference genome
print STDERR "Looking for FASTA references in $genome_dir";
opendir(GEN,$genome_dir) or die "Can't open $genome_dir: $!";
my %reference_sequence_files = map { (my $chr = $_) =~ /(?:chr)?(\S+)\.fa(?:\.gz)?$/ => File::Spec->catfile($genome_dir,$_) } grep{ /\.fa(?:\.gz)?$/ } readdir(GEN);
closedir(GEN);

# Print friendly message with found references
print STDERR "References found : \n".join("\n", map { "\t- ".$_." => ".$reference_sequence_files{$_}} keys %reference_sequence_files),"\n";

# Create the reference genome
print STDERR "Calculating references length\n";
my $genome = CracTools::SimCT::Genome->new(
 reference_sequence_files => \%reference_sequence_files,
);

# Load annotations
print STDERR "Loading annotations\n";
# Create the annotations
my $annotations = CracTools::SimCT::Annotations->new();
$annotations->loadGTF($gtf_file,$genome);

# Create the root clone
print STDERR "Building GenomeSimulator (reading annotations)\n";
my $germline_genome = CracTools::SimCT::GenomeSimulator->new(
 genome => $genome,
);

# Generate Homozygous mutations (random)
my $mutation_generator = CracTools::SimCT::MutationGenerator::Random->new(
 genome_simulator  => $germline_genome,
 ins_rate          => $ins_rate,
 del_rate          => $del_rate,
 sub_rate          => $sub_rate,
);

# Generate germline mutations
print STDERR "Generate random mutations (ins,del,sub) for the Germline genome\n";
$mutation_generator->generateMutations();

# Reset mutation rate for clonal population
$mutation_generator->ins_rate(0);
$mutation_generator->del_rate(0);
$mutation_generator->sub_rate($clone_sub_rate);

print STDERR "Generate random mutations (ins,del,sub) for clone genomes\n";
mkdir $output_dir;
my %mut_hash;
generateClones($nb_generations-1,$germline_genome,"A");
generateClones($nb_generations-1,$germline_genome,"B");


sub generateClones {
  my ($nb_generations, $parent_clone, $name) = @_;
  # Create the clone genome
  my $clone_genome = CracTools::SimCT::GenomeSimulator->new(
   genome => $genome,
  );
  foreach my $mut ($parent_clone->allMutations) {
    $clone_genome->addMutation($mut);
  }
  # Generate mutations for the clone genome
  $mutation_generator->genome_simulator($clone_genome);
  $mutation_generator->generateMutations();
  if($nb_generations == 0) {
    my $simulated_genome_dir  = File::Spec->catfile($output_dir, $name);
    my $simulated_annotations = File::Spec->catfile($output_dir, $name);
    mkdir $simulated_genome_dir;
    my $simulated_genome = $clone_genome->generateGenome(
      genome_dir => $simulated_genome_dir,
      annotations => $annotations,
    );
    # Place mutations in the common hash
    foreach my $mut ($clone_genome->allMutations) {
      my $mut_pos_key = $mut->chr."@".$mut->start;
      my $mut_key     = $mut->reference_sequence."@".$mut->mutation_sequence;
      $mut_hash{$mut_pos_key}{$mut_key}{mut} = $mut;
      push @{$mut_hash{$mut_pos_key}{$mut_key}{clone_names}}, $name;
    }
  } else {
    generateClones($nb_generations-1,$clone_genome, $name."A");
    generateClones($nb_generations-1,$clone_genome, $name."B");
  }
}

my $mutations_output = File::Spec->catfile($output_dir,"mutations.vcf.gz");
my $mutations_fh     = CracTools::Utils::getWritingFileHandle($mutations_output);

my @mut_keys = sort { my ($chrA,$posA) = split("@",$a); my ($chrB,$posB) = split("@",$b); return $chrA cmp $chrB || $posA <=> $posB} keys %mut_hash;
foreach my $mut_pos_key (@mut_keys) {
  my @mutations = map { $_->{mut} } values $mut_hash{$mut_pos_key};
  my @mutations_clones = map { $_->{clone_names} } values $mut_hash{$mut_pos_key};

  # Get raw VCF record
  my $vcf_line = $mutations[0]->getVCFRecord;
  $vcf_line->{info}->{CLONE} = [join("|", @{$mutations_clones[0]})];

  for(my $i = 1; $i < @mutations; $i++) {
    $vcf_line->{alt} .= ",".$mutations[$i]->mutation_sequence;
    push @{$vcf_line->{info}->{CLONE}}, join("|", @{$mutations_clones[$i]});
  }

  CracTools::SimCT::Utils::printVCFLine($mutations_fh,$vcf_line);
}
